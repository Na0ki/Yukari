apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

ext {
    supportLibVersion = '25.4.0'
    playServicesVersion = '11.4.0'
    twitter4jVersion = '4.0.5'
    mastodon4jVersion = '1.6.0'
    leakCanaryVersion = '1.5.4'
}

dependencies {
    // Support Library
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:palette-v7:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:exifinterface:$supportLibVersion"
//    compile "com.android.support:multidex:1.0.2"

    // Play Services
    implementation "com.google.android.gms:play-services-drive:${playServicesVersion}"

    // UI互換
    implementation 'com.github.rey5137:material:1.2.4'
    implementation 'com.github.machinarius:preferencefragment:0.1.1'

    // UI追加
    implementation 'com.larswerkman:HoloColorPicker:1.4'
    implementation 'com.code-troopers.betterpickers:library:3.1.0'
    implementation project(':GalleryMultiPicker:library')

    // Twitter4J
//    compile "org.twitter4j:twitter4j-core:${twitter4jVersion}"
//    compile "org.twitter4j:twitter4j-media-support:${twitter4jVersion}"
//    compile "org.twitter4j:twitter4j-stream:${twitter4jVersion}"
//    compile "org.twitter4j:twitter4j-http2-support:${twitter4jVersion}"
    implementation "com.github.takke.twitter4j:twitter4j-core:2612db7f15"
    implementation "com.github.takke.twitter4j:twitter4j-media-support:2612db7f15"
    implementation "com.github.takke.twitter4j:twitter4j-stream:2612db7f15"
    implementation "com.github.takke.twitter4j:twitter4j-http2-support:2612db7f15"

    // Mastodon
    implementation "com.github.sys1yagi.mastodon4j:mastodon4j:${mastodon4jVersion}"

    // パーサ
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'org.jsoup:jsoup:1.8.3'
    implementation 'com.google.zxing:javase:3.2.1', {
        exclude group: 'com.beust', module: 'jcommander'
    }
    implementation 'com.twitter:twitter-text:1.13.0'
    implementation 'com.github.wakamesoba98:totoridipjp4j:11607b8da4'
    implementation project(':twpfparser')

    // 通信
    implementation 'com.squareup.retrofit2:retrofit:2.2.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.2.0'

    // プラグインシステム
    if (getProperty("yukari.exvoice.build").toBoolean()) {
        implementation project(':yukari-exvoice'), {
            exclude group: 'org.twitter4j', module: 'twitter4j-core'
        }
    } else {
        implementation 'info.shibafu528.yukari:yukari-exvoice:20180401.091215', {
            exclude group: 'org.twitter4j', module: 'twitter4j-core'
        }
    }

    // Annotation Processor
    implementation 'com.jakewharton:butterknife:8.8.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.0'
    annotationProcessor 'org.projectlombok:lombok:1.16.4'
    compileOnly 'org.projectlombok:lombok:1.16.4'
    implementation project(':yukari-processor-runtime')
    annotationProcessor project(':yukari-processor')

    // デバッグ用
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // いろいろ
    implementation 'com.annimon:stream:1.0.8'
    implementation 'org.eclipse.collections:eclipse-collections-api:7.1.0'
    implementation 'org.eclipse.collections:eclipse-collections:7.1.0'
    implementation fileTree(dir: './libs', includes: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    // Test
    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

android {
    compileSdkVersion 25
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "shibafu.yukari"
        minSdkVersion 14
        targetSdkVersion 22

        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Android Gradle Plugin v0.7.0でビルドが通らない問題の回避
    // http://stackoverflow.com/questions/20673888/duplicate-files-copied-android-studio-0-4-0
    packagingOptions {
        exclude 'LICENSE-EDL-1.0.txt'
        exclude 'LICENSE-EPL-1.0.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        ignore 'MissingTranslation'
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    kotlinOptions {
        apiVersion = '1.1'
    }
}

// Kotlin
buildscript {
    ext.kotlin_version = '1.2.30'
    ext.kotlin_coroutines_version = '0.22.5'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
