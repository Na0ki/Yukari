apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    mavenCentral()
    maven {
        url 'http://repository.mumei-himazin.info/nexus/content/repositories/releases'
    }
    maven {
        url "https://jitpack.io"
    }
}

ext {
    supportLibVersion = '25.4.0'
    playServicesVersion = '11.4.0'
    twitter4jVersion = '4.0.5'
    mastodon4jVersion = '1.4.0'
    leakCanaryVersion = '1.5.4'
}

dependencies {
    // Support Library
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:palette-v7:$supportLibVersion"
    compile "com.android.support:support-v4:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:exifinterface:$supportLibVersion"
//    compile "com.android.support:multidex:1.0.2"

    // Play Services
    compile "com.google.android.gms:play-services-drive:${playServicesVersion}"

    // UI互換
    compile 'com.github.rey5137:material:1.2.4'
    compile 'com.github.machinarius:preferencefragment:0.1.1'

    // UI追加
    compile 'com.larswerkman:HoloColorPicker:1.4'
    compile 'com.code-troopers.betterpickers:library:3.1.0'
    compile project(':GalleryMultiPicker:library')

    // Twitter4J
//    compile "org.twitter4j:twitter4j-core:${twitter4jVersion}"
//    compile "org.twitter4j:twitter4j-media-support:${twitter4jVersion}"
//    compile "org.twitter4j:twitter4j-stream:${twitter4jVersion}"
//    compile "org.twitter4j:twitter4j-http2-support:${twitter4jVersion}"
    compile "com.github.takke.twitter4j:twitter4j-core:2612db7f15"
    compile "com.github.takke.twitter4j:twitter4j-media-support:2612db7f15"
    compile "com.github.takke.twitter4j:twitter4j-stream:2612db7f15"
    compile "com.github.takke.twitter4j:twitter4j-http2-support:2612db7f15"

    // Mastodon
    compile "com.github.sys1yagi.mastodon4j:mastodon4j:${mastodon4jVersion}"

    // パーサ
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'com.google.zxing:javase:3.2.1', {
        exclude group: 'com.beust', module: 'jcommander'
    }
    compile 'com.twitter:twitter-text:1.13.0'
    compile 'com.github.wakamesoba98:totoridipjp4j:11607b8da4'
    compile project(':twpfparser')

    // 通信
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'

    // プラグインシステム
    if (getProperty("yukari.exvoice.build").toBoolean()) {
        compile project(':yukari-exvoice'), {
            exclude group: 'org.twitter4j', module: 'twitter4j-core'
        }
    } else {
        compile 'info.shibafu528.yukari:yukari-exvoice:20180116.230029', {
            exclude group: 'org.twitter4j', module: 'twitter4j-core'
        }
    }
    compile 'twitter.intent:twitter-intent-core:1.0,0'
    compile 'twitter.intent:twitter-intent-t4j:1.0.0', {
        exclude group: 'org.twitter4j', module: 'twitter4j-core'
    }

    // Annotation Processor
    compile 'com.jakewharton:butterknife:8.8.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.0'
    provided 'org.projectlombok:lombok:1.16.4'
    compile project(':yukari-processor-runtime')
    annotationProcessor project(':yukari-processor')

    // デバッグ用
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // いろいろ
    compile 'com.annimon:stream:1.0.8'
    compile 'org.eclipse.collections:eclipse-collections-api:7.1.0'
    compile 'org.eclipse.collections:eclipse-collections:7.1.0'
    compile fileTree(dir: './libs', includes: ['*.jar'])

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Test
    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "shibafu.yukari"
        minSdkVersion 14
        targetSdkVersion 22

        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Android Gradle Plugin v0.7.0でビルドが通らない問題の回避
    // http://stackoverflow.com/questions/20673888/duplicate-files-copied-android-studio-0-4-0
    packagingOptions {
        exclude 'LICENSE-EDL-1.0.txt'
        exclude 'LICENSE-EPL-1.0.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        ignore 'MissingTranslation'
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    kotlinOptions {
        apiVersion = '1.1'
    }
}

// Kotlin
buildscript {
    ext.kotlin_version = '1.1.60'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
