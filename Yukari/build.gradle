apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    mavenCentral()
    maven {
        url 'http://shibafu528.github.io/totoridipjp4J/build/repo'
    }
    maven {
        url 'http://repository.mumei-himazin.info/nexus/content/repositories/releases'
    }
    maven {
        url "https://jitpack.io"
    }
}

ext {
    supportLibVersion = '25.2.0'
    playServicesVersion = '6.5.87'
    twitter4jVersion = '4.0.5'
    leakCanaryVersion = '1.5'
}

dependencies {
    // Support Library
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:palette-v7:$supportLibVersion"
    compile "com.android.support:support-v4:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:multidex:1.0.1"

    // Play Services
    compile "com.google.android.gms:play-services-plus:${playServicesVersion}"
    compile "com.google.android.gms:play-services-drive:${playServicesVersion}"

    // UI互換
    compile 'com.github.rey5137:material:1.2.4'
    compile 'com.github.machinarius:preferencefragment:0.1.1'

    // UI追加
    compile 'com.larswerkman:HoloColorPicker:1.4'
    compile 'com.code-troopers.betterpickers:library:3.1.0'
    compile project(':GalleryMultiPicker:library')

    // Twitter4J
//    compile "org.twitter4j:twitter4j-core:${twitter4jVersion}"
//    compile "org.twitter4j:twitter4j-async:${twitter4jVersion}"
//    compile "org.twitter4j:twitter4j-media-support:${twitter4jVersion}"
//    compile "org.twitter4j:twitter4j-stream:${twitter4jVersion}"
//    compile "org.twitter4j:twitter4j-http2-support:${twitter4jVersion}"
    compile "com.github.takke.twitter4j:twitter4j-core:master_based_dev-SNAPSHOT"
    compile "com.github.takke.twitter4j:twitter4j-async:master_based_dev-SNAPSHOT"
    compile "com.github.takke.twitter4j:twitter4j-media-support:master_based_dev-SNAPSHOT"
    compile "com.github.takke.twitter4j:twitter4j-stream:master_based_dev-SNAPSHOT"
    compile "com.github.takke.twitter4j:twitter4j-http2-support:master_based_dev-SNAPSHOT"

    // パーサ
    compile 'com.google.code.gson:gson:2.3'
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'com.google.zxing:javase:3.2.1'
    compile 'com.twitter:twitter-text:1.13.0'
    compile 'jp.dip.totori:totoridipjp4J-android:1.0'
    compile project(':twpfparser')

    // プラグインシステム
    compile project(':yukari-exvoice')
    compile 'twitter.intent:twitter-intent-core:1.0,0'
    compile 'twitter.intent:twitter-intent-t4j:1.0.0', {
        exclude group: 'org.twitter4j', module: 'twitter4j-core'
    }

    // Annotation Processor
    compile 'com.jakewharton:butterknife:5.1.2' // TODO: ButterKnife 7がリリースされている。 http://qiita.com/yyaammaa/items/408bc3fe429175fd74ff
    provided 'org.projectlombok:lombok:1.16.4'
    compile project(':yukari-processor-runtime')
    annotationProcessor project(':yukari-processor')

    // デバッグ用
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // いろいろ
    compile 'com.annimon:stream:1.0.8'
    compile fileTree(dir: './libs', includes: ['*.jar'])

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Test
    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "shibafu.yukari"
        minSdkVersion 14
        targetSdkVersion 22

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Android Gradle Plugin v0.7.0でビルドが通らない問題の回避
    // http://stackoverflow.com/questions/20673888/duplicate-files-copied-android-studio-0-4-0
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
//        checkReleaseBuilds false
//        abortOnError false
        ignore 'MissingTranslation'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    productFlavors {
        dev {
            minSdkVersion 21
        }
        prod {
            minSdkVersion 14
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    kotlinOptions {
        apiVersion = '1.1'
    }
}

// Kotlin
buildscript {
    ext.kotlin_version = '1.1.0'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
