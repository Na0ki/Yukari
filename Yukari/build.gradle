apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "http://twitter4j.org/maven2"
    }
}

class GitUtil {
    static def getRevHash() {
        def proc = ["git", "rev-parse", "HEAD"].execute()
        proc.waitFor()
        return proc.inputStream.readLines().first()
    }

    static def getRevCount() {
        def proc = ["git", "rev-list", "--count", "HEAD"].execute()
        proc.waitFor()
        return proc.inputStream.readLines().first()
    }

    static def isDirty() {
        def proc = ["git", "status", "--porcelain"].execute()
        proc.waitFor()
        return proc.inputStream.readLines().size() > 0
    }
}

ext {
    supportLibVersion = '25.4.0'
    playServicesVersion = '11.4.0'
    twitter4jVersion = '4.0.7-SNAPSHOT'
    mastodon4jVersion = '1.6.0'
    leakCanaryVersion = '1.5.4'

    y4aVersionMajor = 2
    y4aVersionMinor = 1
    y4aVersionPatch = 0
    y4aVersionSuffix = '-dev'
    y4aCodeName = 'linkage'

    y4aRevision = GitUtil.revHash.substring(0, 7)
    if (GitUtil.isDirty()) {
        y4aRevision += "*"
    }
    y4aBuildDate = new Date().format("yyMMdd")
    y4aVersionString = [y4aVersionMajor, y4aVersionMinor, y4aVersionPatch, GitUtil.revCount].join(".") + y4aVersionSuffix
    y4aVersionCode = y4aVersionMajor * 1000000 + y4aVersionMinor * 1000 + y4aVersionPatch
    y4aVersionName = "${y4aVersionString} (${y4aCodeName} ${y4aBuildDate} ${y4aRevision})"
}

android {
    compileSdkVersion 25
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "shibafu.yukari"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode y4aVersionCode
        versionName y4aVersionName

        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Android Gradle Plugin v0.7.0でビルドが通らない問題の回避
    // http://stackoverflow.com/questions/20673888/duplicate-files-copied-android-studio-0-4-0
    packagingOptions {
        exclude 'LICENSE-EDL-1.0.txt'
        exclude 'LICENSE-EPL-1.0.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        ignore 'MissingTranslation'
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    flavorDimensions "stage"
    productFlavors {
        main {
            dimension "stage"
        }
        alpha {
            dimension "stage"
            applicationIdSuffix ".alpha"
        }
    }
    applicationVariants.all { variant ->
        variant.resValue "string", "applicationId", variant.applicationId
    }
    kotlinOptions {
        apiVersion = '1.1'
    }
}

dependencies {
    // Support Library
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:palette-v7:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:exifinterface:$supportLibVersion"
//    compile "com.android.support:multidex:1.0.2"

    // Play Services
    implementation "com.google.android.gms:play-services-drive:${playServicesVersion}"

    // UI互換
    implementation 'com.github.rey5137:material:1.2.4'
    implementation 'com.github.machinarius:preferencefragment:0.1.1'

    // UI追加
    implementation 'com.larswerkman:HoloColorPicker:1.4'
    implementation 'com.code-troopers.betterpickers:library:3.1.0'
    implementation project(':GalleryMultiPicker:library')

    // Twitter4J
    compile "org.twitter4j:twitter4j-core:${twitter4jVersion}"
    compile "org.twitter4j:twitter4j-media-support:${twitter4jVersion}"
    compile "org.twitter4j:twitter4j-stream:${twitter4jVersion}"
    compile "org.twitter4j:twitter4j-http2-support:${twitter4jVersion}"
//    implementation "com.github.takke.twitter4j:twitter4j-core:2612db7f15"
//    implementation "com.github.takke.twitter4j:twitter4j-media-support:2612db7f15"
//    implementation "com.github.takke.twitter4j:twitter4j-stream:2612db7f15"
//    implementation "com.github.takke.twitter4j:twitter4j-http2-support:2612db7f15"

    // Mastodon
//    implementation "com.github.sys1yagi.mastodon4j:mastodon4j:${mastodon4jVersion}"
    implementation "com.github.shibafu528.mastodon4j:mastodon4j:yukari-SNAPSHOT"

    // パーサ
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'org.jsoup:jsoup:1.8.3'
    implementation 'com.google.zxing:javase:3.2.1', {
        exclude group: 'com.beust', module: 'jcommander'
    }
    implementation 'com.twitter:twitter-text:1.13.0'
    implementation 'com.github.wakamesoba98:totoridipjp4j:11607b8da4'
    implementation project(':twpfparser')

    // 通信
    implementation 'com.squareup.retrofit2:retrofit:2.2.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.2.0'

    // プラグインシステム
    if (getProperty("yukari.exvoice.build").toBoolean()) {
        implementation project(':yukari-exvoice'), {
            exclude group: 'org.twitter4j', module: 'twitter4j-core'
        }
    } else {
        implementation 'info.shibafu528.yukari:yukari-exvoice:20180513.002641', {
            exclude group: 'org.twitter4j', module: 'twitter4j-core'
        }
    }

    // Annotation Processor
    implementation 'com.jakewharton:butterknife:8.8.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.0'
    implementation project(':yukari-processor-runtime')
    annotationProcessor project(':yukari-processor')

    // デバッグ用
    debugImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // Deploygate SDK
    alphaImplementation 'com.deploygate:sdk:4.1.0'

    // いろいろ
    implementation 'com.annimon:stream:1.0.8'
    implementation 'org.eclipse.collections:eclipse-collections-api:7.1.0'
    implementation 'org.eclipse.collections:eclipse-collections:7.1.0'
    implementation fileTree(dir: './libs', includes: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    // Test
    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

// Kotlin
buildscript {
    ext.kotlin_version = '1.2.60'
    ext.kotlin_coroutines_version = '0.24.0'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

task showVersion {
    println "VersionCode: " + y4aVersionCode
    println "VersionName: " + y4aVersionName
}